//ESS_LOCALE SimplifiedChinese_China.MS936@Binary

SET UPDATECALC OFF;
SET AGGMISSG ON;
SET CREATENONMISSINGBLK OFF; 

/*
维度总量 19个
其中CDD01-CDD06为备用维度尚未启用
密集维：Account/Value/View
稀疏维：Period/Entity/Productline/Product/Channel/Customer/Region/Version/Scenario/Years
*/

/*反跑月数据，有数据后，每个月改PreMon变量即可*/

FIX (&CurYr, "Actual","Working","no_CDD01","no_CDD02","no_CDD03","no_CDD04","no_CDD05","no_CDD06") /*(9)*/

	FIX("Amount" ,@RELATIVE("Entity",0))/*(11)*/	
	
		/*对分摊因子进行聚合："销售收入","销售成本","产量","销量"*/	
		FIX("销售收入","销售成本","销量","Before_Allc")/*(13)*/					
			AGG("Product");
			AGG("ProductLine");
			AGG("Channel");
			AGG("Customer");
			AGG("Region");
			AGG("Period");
		ENDFIX
		
		/*将"销量","销售收入","销售成本"的“Before_Allc”数据copy到“Orig”*/
		FIX(@RELATIVE("Product",0),@RELATIVE("ProductLine",0))/*(13)*/
			FIX(@IDESC(&PreMon,0),@RELATIVE("Customer",0),@RELATIVE("Region",0),@RELATIVE("Channel",0),"销售收入","销售成本","销量")/*(18)*/
				DATACOPY "Before_Allc" TO "Orig";
			ENDFIX
	
		/*将"产量"的“Before_Allc”数据copy到“Orig”*/		
			FIX("no_Channel","no_Customer","no_Region","产量")/*(17)*/
				FIX ("Before_Allc")/*(18)*/				
					AGG("Period");
				ENDFIX
				FIX(@IDESC(&PreMon,0))/*(18)*/
					DATACOPY "Before_Allc" TO "Orig";
				ENDFIX	
			ENDFIX	
		ENDFIX
	ENDFIX

	FIX(@RELATIVE("Product",0),@RELATIVE("ProductLine",0),@RELATIVE("Customer",0),@RELATIVE("Region",0),@RELATIVE("Channel",0),@RELATIVE("Entity",0))/*(15)*/
	
		/*"销售成本调整项"根据"销售成本"占比分摊到各SKU上，数据存储在"销售成本调整项"->"Orig"上。最终"Orig"上"销售成本合计"="销售成本"+"销售成本调整项"（大纲动态计算）*/
		FIX(@RELATIVE(&PreMon,0),"Orig")/*(17)*/
			FIX("Amount") /*(18)*/		
				"销售成本调整项"="销售成本"->"Before_Allc"/"销售成本"->"Before_Allc"->"All_Customer"->"All_ProductLine"->"All_Region"->"All_Channel"->"All_Product"->&PreMon*"销售成本调整项"->"no_Customer"->"no_Region"->"no_ProductLine"->"no_Channel"->"no_Product"->"Before_Allc"->@PARENT("Period")->&PreMon/"Days"->"预算"->"no_Product"->"no_ProductLine"->"no_Channel"->"no_Region"->"no_Customer"->"no_Entity"->@PARENT("Period")->&PreMon;	
			ENDFIX			
			FIX("Per_Unit") /*(18)*/
				"销售成本调整项" = "销售成本调整项"->"Amount"/"销量"->"Amount";
			ENDFIX	
		ENDFIX
		
		/*"销售成本调整项"日数据聚合到月总量*/
		FIX("Amount","Orig","销售成本调整项") /*(18)*/
			@IDESC("YearTotal");	
		ENDFIX
		
		/*"销售成本调整项"月总量除以销量得出单位数据*/
		FIX(&PreMon,"Orig","Per_Unit")/*(18)*/
				"销售成本调整项" = "销售成本调整项"->"Orig"->"Amount"/"销量"->"Amount";
		ENDFIX

		/*根据销售收入占比对销管财费用等科目进行分摊（不含”跌价准备“）*/
		/*根据销售收入占比对"销售费用","销售费用-运输费用","销售费用-样品费用","销售费用-品牌使用费","税金及附加","管理费用","管理费用-折旧费用","管理费用-薪资及附加","管理费用-工会经费","财务费用","财务费用-利息支出","财务费用-利息收入","财务费用-银行手续费","财务费用-财务服务费","财务费用-汇兑损益","财务费用-锁定利率","其他业务利润","营业外收支","技术服务费","锁汇资金占用"进行分摊*/
		FIX("销售费用","销售费用-运输费用","销售费用-样品费用","销售费用-品牌使用费","税金及附加","管理费用","管理费用-折旧费用","管理费用-薪资及附加","管理费用-工会经费","财务费用","财务费用-利息支出","财务费用-利息收入","财务费用-银行手续费","财务费用-财务服务费","财务费用-汇兑损益","财务费用-锁定利率","其他业务利润","营业外收支","技术服务费")	/*(16)*/	
			/*上月Orig月费用按本月天数均摊到本月每日Orig*/
				FIX(@RELATIVE(&PreMon,0))/*(17)*/	
				FIX("Amount")/*(18)*/	
					"Orig"="Before_Allc"->"no_Product"->"no_ProductLine"->"no_Channel"->"no_Region"->"no_Customer"->@PARENT("Period")->&PreMon/"Days"->"预算"->"no_Product"->"no_ProductLine"->"no_Channel"->"no_Region"->"no_Customer"->"no_Entity"->@PARENT("Period")->&PreMon*"销售收入"/"销售收入"->"All_Customer"->"All_ProductLine"->"All_Region"->"All_Channel"->"All_Product"->"Before_Allc"->&PreMon;
				ENDFIX
				/*单位=总额/销量,日*/				
				FIX("Per_Unit")/*(18)*/	
					"Orig"="Orig"->"Amount"/"销量"->"Amount";
				ENDFIX
			ENDFIX
			
			/*相关日数据聚合到月总量*/		
			FIX("orig","Amount")/*18*/
				@IDESC("YearTotal");
			ENDFIX
			
			/*单位=总额/销量,月*/		
			FIX(&PreMon,"Per_Unit")/*(18)*/	
				"Orig"="Orig"->"Amount"/"销量"->"Amount";
			ENDFIX
		ENDFIX

		FIX("肉类成本","米面成本","蔬菜成本","其他原料成本","辅料成本","包装成本","原料成本","制造费用","直接人工","直接水费","直接电费","直接维修","直接物料消耗","直接蒸汽","直接天然气","直接折旧","直接其他","间接人工","间接水费","间接电费","间接维修","间接物料消耗","间接蒸汽","间接天然气","间接折旧","间接其他")/*(16)*/
			FIX(@RELATIVE(&PreMon,0))/*(17)*/
				/*上月成本Orig按产量均摊到本月每日Orig*/		
				FIX("Per_Unit")/*(18)*/
					"Orig"="Amount"->"Before_Allc"->"no_Channel"->"no_Customer"->"no_Region"->@PARENT("Period")->&PreMon/"产量"->"Amount"->"no_Channel"->"no_Customer"->"no_Region"->"Before_Allc"->@PARENT("Period")->&PreMon;
				ENDFIX
				FIX("Amount")/*(18)*/
					"Orig"="Per_Unit"*"销量";
				ENDFIX
			ENDFIX
			/*相关日数据聚合到月总量*/			
			FIX("orig","Amount")/*18*/
				@IDESC("YearTotal");
			ENDFIX
			/*单位生产成本=总额/产量，月*/		
			FIX(&PreMon,"Per_Unit")/*(18)*/
				"Orig"="Amount"/"销量"->"Amount";
			ENDFIX	
		ENDFIX

		/*销售成本 单位=总额/产量 日总量数据直接接口进入，单位数据需计算 */
		FIX("销售成本","销售收入")/*(16)*/
			FIX(@RELATIVE(&PreMon,0),"Per_Unit")/*(18)*/
				"Orig"="Orig"->"Amount" /"销量"->"Amount";
			ENDFIX
		/*相关日数据聚合到月总量*/		
			FIX("Orig","Amount")/*18*/
				@IDESC("YearTotal");
			ENDFIX

		/*销售成本 单位=总额/产量 月 */			
			FIX(&PreMon,"Per_Unit")/*(18)*/
				"Orig"="Orig"->"Amount" / "销量"->"Amount";
			ENDFIX
		ENDFIX

		/*单位=总额/产量 日/月 */
		/*产销成本差异=单位生产成本*产量-销售成本合计*/
		FIX(@RELATIVE(&PreMon,0),"Orig")/*(17)*/
			FIX("Amount")/*(18)*/
				"产销成本差异"="生产成本"->"no_Channel"->"no_Customer"->"no_Region"->"Per_Unit" * "产量"->"no_Channel"->"no_Customer"->"no_Region" -"销售成本合计";
			ENDFIX
			FIX("Per_Unit")/*(18)*/
				"产销成本差异"="产销成本差异" ->"Amount"/"销量"->"Amount";
			ENDFIX
		ENDFIX
		
		FIX(&PreMon)/*(17)*/
			FIX("Amount")/*(18)*/
				"产销成本差异"="生产成本" - "销售成本合计";
			ENDFIX
			FIX("Per_Unit")/*(18)*/
				"产销成本差异"="产销成本差异" ->"Amount"/ "销量"->"Amount";
			ENDFIX
		ENDFIX


		/*边际贡献="销售收入"->"Amount"/"销量"->"Amount" -"原料成本" -"辅料成本" -"包装成本"-"变动制造费用"-"销售费用" 只计有销售收入的*/
		FIX(@RELATIVE(&PreMon,0),"Orig")/*(17)*/
			FIX("Per_Unit")/*(18)*/
				"边际贡献"
				(
					IF("销售收入"->"Amount"<> #MISSING)
						"边际贡献" = "销售收入"->"Amount"/"销量"->"Amount" -"原料成本" -"辅料成本" -"包装成本"-"变动制造费用"-"销售费用";
					ENDIF
				)
				
			ENDFIX
			FIX("Amount")/*(18)*/
				"边际贡献"
				(
					IF("销售收入"->"Amount"<> #MISSING)
						"边际贡献" = "边际贡献"->Per_Unit * "销量";
					ENDIF
				)
			ENDFIX	
		ENDFIX
		
		FIX(&PreMon,"Orig")/*(17)*/
			FIX("Per_Unit")/*(18)*/
				"边际贡献"
				(
					IF("销售收入"->"Amount"<> #MISSING)
						"边际贡献" = "销售收入"->"Amount"/"销量"->"Amount" -"原料成本" -"辅料成本"-"包装成本" -"变动制造费用"-"销售费用";
					ENDIF
				)
			ENDFIX
			FIX("Amount")/*(18)*/
				"边际贡献"
				(
					IF("销售收入"->"Amount"<> #MISSING)
						"边际贡献" = "边际贡献"->Per_Unit * "销量";
					ENDIF
				)
			ENDFIX
		ENDFIX
	ENDFIX

/*各维度大聚合,单位=总额/销量*/
	FIX(@IDESC(&PreMon),@RELATIVE("AF_P&L",0),@RELATIVE("Quantity",0),"销售费用-运输费用","销售费用-样品费用","销售费用-品牌使用费","管理费用-折旧费用","管理费用-薪资及附加","管理费用-工会经费","财务费用-利息支出","财务费用-利息收入","财务费用-银行手续费","财务费用-财务服务费","产销成本差异","边际贡献","税金及附加","肉类成本","米面成本","蔬菜成本","其他原料成本","辅料成本","包装成本","原料成本","制造费用","直接人工","直接水费","直接电费","直接维修","直接物料消耗","直接蒸汽","直接天然气","直接折旧","直接其他","间接人工","间接水费","间接电费","间接维修","间接物料消耗","间接蒸汽","间接天然气","间接折旧","间接其他","销量")/*(11)*/			
		FIX("Amount","Orig")/*(13)*/
			AGG("Entity");
			AGG("Product");
			AGG("Channel");
			AGG("Customer");
			AGG("Region");
			AGG("ProductLine");
		ENDFIX

		FIX("Per_Unit",@IDESC("All_Entity"),@IDESC("All_Product"),@IDESC("All_Channel"),@IDESC("All_Customer"),@IDESC("All_Region"),@IDESC("All_ProductLine"))/*(18)*/
			"Orig"="Amount"/"销量"->"Amount";
		ENDFIX

	ENDFIX	
ENDFIX

