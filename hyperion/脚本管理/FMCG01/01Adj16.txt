//ESS_LOCALE SimplifiedChinese_China.MS936@Binary

SET UPDATECALC OFF;
SET AGGMISSG ON;
SET CREATENONMISSINGBLK OFF; 

/*
维度总量 19个
其中CDD01-CDD06为备用维度尚未启用
密集维：Account/Value/View
稀疏维：Period/Entity/Productline/Product/Channel/Customer/Region/Version/Scenario/Years
*/
FIX (&CurYr, "Actual","Working","no_CDD01","no_CDD02","no_CDD03","no_CDD04","no_CDD05","no_CDD06") /*(9)*/

	FIX("Amount" ,@RELATIVE("Entity",0))/*(11)*/	
	
		/*对分摊因子进行聚合："销售收入","销售成本","产量","销量"*/	
		FIX("销售收入","销售成本","销量","Before_Allc")/*(13)*/					
			AGG("Product");
			AGG("ProductLine");
			AGG("Channel");
			AGG("Customer");
			AGG("Region");
			AGG("Period");
		ENDFIX
		
		/*将"销量","销售收入","销售成本"的“Before_Allc”数据copy到“Orig”*/
		FIX(@RELATIVE("Product",0),@RELATIVE("ProductLine",0))/*(13)*/
			FIX(@IDESC("Period"),@RELATIVE("Customer",0),@RELATIVE("Region",0),@RELATIVE("Channel",0),"销售收入","销售成本","销量")/*(18)*/
				DATACOPY "Before_Allc" TO "Orig";
			ENDFIX

			/*删除边际贡献*/
			FIX(@IDESC("Period"),@RELATIVE("Customer",0),@RELATIVE("Region",0),@RELATIVE("Channel",0),"边际贡献")/*(18)*/
				"Orig" = #MISSING;
			ENDFIX
			
			/*删除成本*/			FIX(@RELATIVE("Customer",0),@RELATIVE("Region",0),@RELATIVE("Channel",0),"肉类成本","米面成本","蔬菜成本","其他原料成本","辅料成本","包装成本","原料成本","制造费用","直接人工","直接水费","直接电费","直接维修","直接物料消耗","直接蒸汽","直接天然气","直接折旧","直接其他","间接人工","间接水费","间接电费","间接维修","间接物料消耗","间接蒸汽","间接天然气","间接折旧","间接其他")
				FIX(@IDESC("YearTotal"))/*(18)*/
					"Orig" = #MISSING;
				ENDFIX
			ENDFIX
		
			/*将"产量"等的“Before_Allc”数据copy到“Orig”*/		
			FIX("no_Channel","no_Customer","no_Region","产量")/*(17)*/
				FIX ("Before_Allc")/*(18)*/				
					AGG("Period");
				ENDFIX
				FIX(@IDESC("Period"))/*(18)*/
					DATACOPY "Before_Allc" TO "Orig";
				ENDFIX	
			ENDFIX	
			
			FIX("no_Channel","no_Customer","no_Region","肉类成本","米面成本","蔬菜成本","其他原料成本","辅料成本","包装成本","原料成本","制造费用","直接人工","直接水费","直接电费","直接维修","直接物料消耗","直接蒸汽","直接天然气","直接折旧","直接其他","间接人工","间接水费","间接电费","间接维修","间接物料消耗","间接蒸汽","间接天然气","间接折旧","间接其他")
				FIX(@CHILD("YearTotal"))/*(18)*/
					DATACOPY "Before_Allc" TO "Orig";
				ENDFIX	
			ENDFIX
		ENDFIX
				
	ENDFIX
	
	/*工厂成本单位总额计算*/		
	FIX(@RELATIVE("Product",0),@RELATIVE("ProductLine",0),"no_Channel","no_Customer","no_Region",@RELATIVE("Entity",0))/*(15)*/	
		FIX("肉类成本","米面成本","蔬菜成本","其他原料成本","辅料成本","包装成本","原料成本","制造费用","直接人工","直接水费","直接电费","直接维修","直接物料消耗","直接蒸汽","直接天然气","直接折旧","直接其他","间接人工","间接水费","间接电费","间接维修","间接物料消耗","间接蒸汽","间接天然气","间接折旧","间接其他")/*(16)*/
			/*单位生产成本=总额/产量，月*/		
			FIX(@CHILD("YearTotal"),"Per_Unit")/*(18)*/
				"Orig"="Amount"->"Before_Allc"/"产量"->"Amount"->"Before_Allc";
			ENDFIX	
			
			FIX(@RELATIVE("YearTotal",0))/*(17)*/
				FIX("Per_Unit")/*(18)*/
					"Orig"
					(
						IF("产量"->"Amount"<> #MISSING)
							"Orig"="Amount"->"Before_Allc"->@PARENT("Period")/"产量"->"Amount"->"Before_Allc"->@PARENT("Period");
						ENDIF
					)
					
				ENDFIX
				/*日总额=单位生产成本*产量（如有）*/	
				FIX("Orig")/*(18)*/
					"Amount"
					(
						IF("产量"->"Amount"<> #MISSING)
							"Amount" = Per_Unit * "产量"->"no_Channel"->"no_Customer"->"no_Region"->"Amount";
						ENDIF
					)
				ENDFIX				
			ENDFIX
		ENDFIX
	ENDFIX
	

	FIX(@RELATIVE("Product",0),@RELATIVE("ProductLine",0),@RELATIVE("Customer",0),@RELATIVE("Region",0),@RELATIVE("Channel",0),@RELATIVE("Entity",0))/*(15)*/
	
		/*"销售成本调整项"根据"销售成本"占比分摊到各SKU上，数据存储在"销售成本调整项"->"Orig"上。最终"Orig"上"销售成本合计"="销售成本"+"销售成本调整项"（大纲动态计算）*/
		FIX(@RELATIVE("YearTotal",0),"Orig")/*(17)*/
			FIX("Amount") /*(18)*/		
				"销售成本调整项"="销售成本"->"Before_Allc"/"销售成本"->"Before_Allc"->"All_Customer"->"All_ProductLine"->"All_Region"->"All_Channel"->"All_Product"->@PARENT("Period")*"销售成本调整项"->"no_Customer"->"no_Region"->"no_ProductLine"->"no_Channel"->"no_Product"->"Before_Allc"->@PARENT("Period");
			ENDFIX			
		ENDFIX
		
		/*"销售成本调整项"日数据聚合到月总量*/
		FIX("Amount","Orig","销售成本调整项") /*(18)*/
			@IDESC("YearTotal");	
		ENDFIX
		

		/*根据销售收入占比对销管财费用等科目进行分摊（不含”跌价准备“）*/
		/*根据销售收入占比对"销售费用","销售费用-运输费用","销售费用-样品费用","销售费用-品牌使用费","税金及附加","管理费用","管理费用-折旧费用","管理费用-薪资及附加","管理费用-工会经费","财务费用","财务费用-利息支出","财务费用-利息收入","财务费用-银行手续费","财务费用-财务服务费","财务费用-汇兑损益","财务费用-锁定利率","其他业务利润","营业外收支","技术服务费","锁汇资金占用"进行分摊*/
		FIX("销售费用","销售费用-运输费用","销售费用-样品费用","销售费用-品牌使用费","税金及附加","管理费用","管理费用-折旧费用","管理费用-薪资及附加","管理费用-工会经费","财务费用","财务费用-利息支出","财务费用-利息收入","财务费用-银行手续费","财务费用-财务服务费","财务费用-汇兑损益","财务费用-锁定利率","其他业务利润","营业外收支","技术服务费","所得税","锁汇资金占用")	/*(16)*/	
			FIX(@RELATIVE("YearTotal",0))/*(17)*/	
				/*上月Orig月费用按本月天数均摊到本月每日Orig*/
				FIX("Amount")/*(18)*/	
					"Orig"="Before_Allc"->@PARENT("Period")->"no_Product"->"no_ProductLine"->"no_Channel"->"no_Region"->"no_Customer"*"销售收入"->"Before_Allc"/"销售收入"->"All_Customer"->"All_ProductLine"->"All_Region"->"All_Channel"->"All_Product"->"Before_Allc"->@PARENT("Period");
				ENDFIX
			ENDFIX
			
			/*相关日数据聚合到月总量*/		
			FIX("orig","Amount")/*18*/
				@IDESC("YearTotal");
			ENDFIX
			
		ENDFIX
		


		/*销售成本 单位=总额/销量 日总量数据直接接口进入，单位数据大纲计算 */
		FIX("销售成本","销售收入")/*(16)*/
		/*相关日数据聚合到月总量*/		
			FIX("Orig","Amount")/*18*/
				@IDESC("YearTotal");
			ENDFIX

		ENDFIX

		/*单位=总额/产量 日/月 */
		/*产销成本差异=单位生产成本*销量-销售成本合计*/
		FIX(@RELATIVE("YearTotal",0),"Orig")/*(17)*/
			FIX("Amount")/*(18)*/
				"产销成本差异"= ("生产成本"->"no_Channel"->"no_Customer"->"no_Region"->"Per_Unit"->@PARENT("Period") - "销售成本合计"->"Per_Unit00") * "销量";
			ENDFIX
		ENDFIX
		
		FIX(@CHILD("YearTotal"))/*(17)*/
			FIX("Amount")/*(18)*/
				"产销成本差异"="生产成本" - "销售成本合计";
			ENDFIX
		ENDFIX

		/*边际贡献="销售收入"->"Amount"/"销量"->"Amount" -"原料成本" -"辅料成本" -"包装成本"-"变动制造费用"-"销售费用" 只计有销售收入的
		1、日：先计算单位边际贡献，再计算总额=单位边际贡献*销量
		2、月：先计算总额=日的聚合，再计算单位边际贡献=月总额/销量*/
		
		FIX(@RELATIVE("YearTotal",0),"Orig")/*(17)*/
		  FIX("Per_Unit")/*(18)*/
		 "边际贡献"
			(
				IF("销售收入"->"Amount"<> #MISSING)
						"边际贡献"="销售收入"->"Amount"/"销量"->"Amount"-"原料成本"->@PARENT("Period")->"no_Channel"->"no_Customer"->"no_Region" -"辅料成本"->@PARENT("Period")->"no_Channel"->"no_Customer"->"no_Region" -"包装成本"->@PARENT("Period")->"no_Channel"->"no_Customer"->"no_Region"-"变动制造费用"->@PARENT("Period")->"no_Channel"->"no_Customer"->"no_Region"-"销售费用"->"Per_Unit00" ;
				ENDIF
			)
			ENDFIX
			
			FIX("Amount")/*(18)*/
				"边际贡献"="边际贡献"->"Per_Unit"*"销量";
			ENDFIX
		ENDFIX
		
		FIX("边际贡献","Orig")/*(17)*/
		/*相关日数据聚合到月总量*/		
			FIX("Amount")/*18*/
				@IDESC("YearTotal");
			ENDFIX
			
		/*月单位边际贡献=月总额/月销量*/	
			FIX(@CHILD(YearTotal))/*(18)*/
				"Per_Unit"="Amount"/"销量"->"Amount";
			ENDFIX	
			
		ENDFIX
		
		/*跌价准备按照销售收入比例分摊*/
		FIX("管理费用-跌价准备")/*(16)*/	
			FIX(@RELATIVE("YearTotal",0))/*(17)*/	
				FIX("Amount")/*(18)*/	
					"Orig" ="销售收入"->"Before_Allc"/"销售收入"->"Before_Allc"->"All_Channel"->"All_Product"->"All_Region"->"All_Customer"->@PARENT("Period")*"Before_Allc"->"no_Channel"->"no_Customer"->"no_Region"->@PARENT("Period");
				ENDFIX
			ENDFIX
		ENDFIX
			
		FIX("Amount","Orig")/*18*/
				@IDESC("YearTotal");
		ENDFIX
		
	ENDFIX
		

/*各维度大聚合,单位=总额/销量*/
	FIX(@IDESC("Yeartotal"),@RELATIVE("AF_P&L",0),@RELATIVE("Quantity",0),"销售费用-运输费用","销售费用-样品费用","销售费用-品牌使用费","管理费用-折旧费用","管理费用-薪资及附加","管理费用-工会经费","财务费用-利息支出","财务费用-利息收入","财务费用-银行手续费","财务费用-财务服务费","产销成本差异","边际贡献","税金及附加","肉类成本","米面成本","蔬菜成本","其他原料成本","辅料成本","包装成本","原料成本","制造费用","直接人工","直接水费","直接电费","直接维修","直接物料消耗","直接蒸汽","直接天然气","直接折旧","直接其他","间接人工","间接水费","间接电费","间接维修","间接物料消耗","间接蒸汽","间接天然气","间接折旧","间接其他","财务费用-汇兑损益","财务费用-锁定利率","所得税","锁汇资金占用","管理费用-跌价准备")/*(11)*/			
		FIX("Amount","Orig")/*(13)*/
			AGG("Entity");
			AGG("Product");
			AGG("Channel");
			AGG("Customer");
			AGG("Region");
			AGG("ProductLine");
		ENDFIX

		FIX(@IDESC("Yeartotal"),"肉类成本","米面成本","蔬菜成本","其他原料成本","辅料成本","包装成本","原料成本","制造费用","直接人工","直接水费","直接电费","直接维修","直接物料消耗","直接蒸汽","直接天然气","直接折旧","直接其他","间接人工","间接水费","间接电费","间接维修","间接物料消耗","间接蒸汽","间接天然气","间接折旧","间接其他","管理费用-跌价准备")/*(11)*/
			FIX("Per_Unit",@IDESC("Total_Product"),@IDESC("Total_ProductLine"))/*(18)*/
				"Orig"="Amount"/"产量"->"no_Channel"->"no_Customer"->"no_Region"->"Amount";
			ENDFIX
		ENDFIX	

	ENDFIX	

ENDFIX

