//ESS_LOCALE SimplifiedChinese_China.MS936@Binary

SET UPDATECALC OFF;
SET AGGMISSG ON;
SET CREATENONMISSINGBLK OFF; 

/*
维度总量 19个
其中CDD01-CDD06为备用维度尚未启用
密集维：Account/Value/View
稀疏维：Period/Entity/Productline/Product/Channel/Customer/Region/Version/Scenario/Years
*/

/*这个脚本是日预测用的 &Curday 有时&CurMon*/


FIX (&CurYr,"Actual","Working","no_CDD01","no_CDD02","no_CDD03","no_CDD04","no_CDD05","no_CDD06") /*(9)*/

	FIX("Amount",@RELATIVE("Entity",0))/*(11)*/	
	
		/*对分摊因子进行聚合："销售收入","销售成本","产量","销量"*/	
		FIX("销售收入","销售成本","销量","Before_Allc")/*(13)*/					
			AGG("Product");
			AGG("ProductLine");
			AGG("Channel");
			AGG("Customer");
			AGG("Region");
			AGG("Period");
		ENDFIX
		
		/*将"销量","销售收入","销售成本"的“Before_Allc”数据copy到“Orig”*/
		FIX(@RELATIVE("Product",0),@RELATIVE("ProductLine",0))/*(13)*/
			FIX(&CurDay,&CurMon,@RELATIVE("Customer",0),@RELATIVE("Region",0),@RELATIVE("Channel",0),"销售收入","销售成本","销量")/*(18)*/
				DATACOPY "Before_Allc" TO "Orig";
			ENDFIX
	
		/*将"产量"的“Before_Allc”数据copy到“Orig”*/		
			FIX("no_Channel","no_Customer","no_Region","产量")/*(17)*/
				FIX ("Before_Allc")/*(18)*/				
					AGG("Period");
				ENDFIX
				FIX(&CurDay,&CurMon)/*(18)*/
					DATACOPY "Before_Allc" TO "Orig";
				ENDFIX	
			ENDFIX	
			
			FIX("no_Channel","no_Customer","no_Region","肉类成本","米面成本","蔬菜成本","其他原料成本","辅料成本","包装成本","原料成本","制造费用","直接人工","直接水费","直接电费","直接维修","直接物料消耗","直接蒸汽","直接天然气","直接折旧","直接其他","间接人工","间接水费","间接电费","间接维修","间接物料消耗","间接蒸汽","间接天然气","间接折旧","间接其他")
				FIX(&CurMon)/*(18)*/
					DATACOPY "Before_Allc" TO "Orig";
				ENDFIX	
			ENDFIX	
		ENDFIX
	ENDFIX
	
	FIX(@RELATIVE("Product",0),@RELATIVE("ProductLine",0),"no_Channel","no_Customer","no_Region",@RELATIVE("Entity",0))/*(15)*/	
	/*生产类日/月单位成本均取上个月单位成本 日总额为单位*当日产量（如有） 月总量聚合*/
		FIX("肉类成本","米面成本","蔬菜成本","其他原料成本","辅料成本","包装成本","原料成本","制造费用","直接人工","直接水费","直接电费","直接维修","直接物料消耗","直接蒸汽","直接天然气","直接折旧","直接其他","间接人工","间接水费","间接电费","间接维修","间接物料消耗","间接蒸汽","间接天然气","间接折旧","间接其他")/*(16)*/
			/*单位生产成本=总额/产量，月*/					
			FIX(&CurMon,"Per_Unit")/*(18)*/
				"Orig"=Orig->&PreMon;
			ENDFIX	
						
			FIX(&CurDay)/*(17)*/
				FIX("Per_Unit")/*(18)*/
					"Orig"
					(
						IF("产量"->"Amount"<> #MISSING)
							"Orig"= Orig->&PreMon;
						ENDIF
					)
				ENDFIX
				/*日总额=单位生产成本*产量（如有）*/	
				FIX("Amount")/*(18)*/
					"Orig"
					(
						IF("产量"<> #MISSING)
							"Orig" = "Orig"->"Per_Unit" * "产量"->"Amount";
						ENDIF
					)
				ENDFIX				
			ENDFIX
					
			/*相关日数据聚合到月总量*/		
			FIX("orig","Amount")/*18*/
				@IDESC(&CurMon);
			ENDFIX
		ENDFIX
	ENDFIX
	
	
	
	
FIX(@RELATIVE("Product",0),@RELATIVE("ProductLine",0),@RELATIVE("Customer",0),@RELATIVE("Region",0),@RELATIVE("Channel",0),@RELATIVE("Entity",0))/*(15)*/
	
		/*日预测不看"销售成本调整项",默认为0；不看"跌价准备"及"产销成本差异"*/

		/*根据销售收入占比对上月"销售费用-运输费用","销售费用-样品费用"进行分摊*/
		/*日单位=上月月总额/月销量总额=上月月单位*/
		/*日总额=日单位*单日销量*/
		/*日总额聚合到月总额，再算月单位*/
		/*单位值用大纲Per_Unit00直接计算*/
		FIX("销售费用-运输费用","销售费用-样品费用")/*(16)*/
			FIX(&CurDay)/*(17)*/	
				FIX("Amount")/*(18)*/
					"Orig"="Before_Allc"->&PreMon->"no_Product"->"no_ProductLine"->"no_Channel"->"no_Region"->"no_Customer"/"销量"->&PreMon->"Amount"->"All_Customer"->"All_ProductLine"->"All_Region"->"All_Channel"->"All_Product"->"Before_Allc"*"销量"->"Before_Allc";
				ENDFIX
				FIX("Per_Unit")/*(18)*/	
					"Orig"="Amount"/"销量"->"Amount"->"Before_Allc";
				ENDFIX
			ENDFIX
			/*相关日数据聚合到月总量*/		
			FIX("orig","Amount")/*18*/
				@IDESC("YearTotal");
			ENDFIX
		ENDFIX

			
		/*"销售费用-品牌使用费"总额=当日销售收入*1.5%*/
		/*单位值用大纲Per_Unit00直接计算*/
		
		FIX("销售费用-品牌使用费")/*(16)*/
			FIX(&CurDay)/*(17)*/	
				FIX("Amount")/*(18)*/	
					"Orig"="销售收入"->"Before_Allc"*0.015;
				ENDFIX
			ENDFIX
			
			/*相关日数据聚合到月总量*/		
			FIX("orig","Amount")/*18*/
				@IDESC("YearTotal");
			ENDFIX
		ENDFIX

		/*根据销售收入占比对上月"销售费用"扣除"销售费用-品牌使用费"进行分摊得出单位值B*/
		/*当日销量*B+当日品牌使用费总额*/
		FIX("销售费用")/*(16)*/
			FIX(&CurDay)/*(17)*/	
				FIX("Amount")/*(18)*/	
					"Orig"=("销售费用"->"Before_Allc"->"Amount"->&PreMon->"no_Product"->"no_ProductLine"->"no_Channel"->"no_Region"->"no_Customer"-"销售费用-品牌使用费"->"Before_Allc"->"Amount"->&PreMon->"no_Product"->"no_ProductLine"->"no_Channel"->"no_Region"->"no_Customer")/"销量"->&PreMon->"Amount"->"All_Customer"->"All_ProductLine"->"All_Region"->"All_Channel"->"All_Product"->"Before_Allc"*"销量"->"Before_Allc"+"销售费用-品牌使用费";
					/*验证月总额差额OK+品牌使用费*/
				ENDFIX
			ENDFIX
			
			/*相关日数据聚合到月总量*/		
			FIX("orig","Amount")/*18*/
				@IDESC("YearTotal");
			ENDFIX
		ENDFIX
		
	/*根据销售收入占比对"税金及附加","管理费用","管理费用-折旧费用","管理费用-薪资及附加","管理费用-工会经费","财务费用","财务费用-利息支出","财务费用-利息收入","财务费用-银行手续费","财务费用-财务服务费","其他业务利润","营业外收支","技术服务费"进行分摊*/
		/*日总额聚合到月总额，再算月单位*/
		/*单位值用大纲Per_Unit00直接计算*/
		/*上月Orig月费用按本月天数均摊到本月每日Orig*/

		FIX("税金及附加","管理费用","管理费用-折旧费用","管理费用-薪资及附加","管理费用-工会经费","财务费用","财务费用-利息支出","财务费用-利息收入","财务费用-银行手续费","财务费用-财务服务费","其他业务利润","营业外收支","技术服务费")/*(16)*/	
			FIX(&CurDay)/*(17)*/	
				FIX("Amount")/*(18)*/	
					"Orig"="Before_Allc"->&PreMon->"no_Product"->"no_ProductLine"->"no_Channel"->"no_Region"->"no_Customer"/"Days"->"预算"->"no_Product"->"no_ProductLine"->"no_Channel"->"no_Region"->"no_Customer"->"no_Entity"->&CurMon->"Orig"*"销售收入"->"Orig"/"销售收入"->"All_Customer"->"All_ProductLine"->"All_Region"->"All_Channel"->"All_Product"->"Before_Allc";
				ENDFIX
				
			ENDFIX
			
			/*相关日数据聚合到月总量*/		
			FIX("orig","Amount")/*18*/
				@IDESC("YearTotal");
			ENDFIX
		ENDFIX	
		
		/*边际贡献="销售收入"->"Amount"/"销量"->"Amount" -"原料成本" -"辅料成本" -"包装成本"-"变动制造费用"-"销售费用" 只计有销售收入的
		1、日：先以上月数据计算单位边际贡献，再计算总额=单位边际贡献*销量
		2、月：先计算总额=日的聚合，再计算单位边际贡献=月总额/销量*/
		
		FIX(&CurDay,"Orig")/*(17)*/
		  FIX("Per_Unit")/*(18)*/
		 "边际贡献"
			(
				IF("销售收入"->"Amount"<> #MISSING)
						"边际贡献"="销售收入"->"Amount"/"销量"->"Amount"-"原料成本"->&PreMon->"no_Channel"->"no_Customer"->"no_Region" -"辅料成本"->&PreMon->"no_Channel"->"no_Customer"->"no_Region" -"包装成本"->&PreMon->"no_Channel"->"no_Customer"->"no_Region"-"变动制造费用"->&PreMon->"no_Channel"->"no_Customer"->"no_Region"-"销售费用"->"Before_Allc"->"Amount"->&PreMon->"no_Product"->"no_ProductLine"->"no_Channel"->"no_Region"->"no_Customer"/"销量"->&PreMon->"Amount"->"All_Customer"->"All_ProductLine"->"All_Region"->"All_Channel"->"All_Product"->"Before_Allc";
				ENDIF
			)
			ENDFIX
			
			FIX("Amount")/*(18)*/
				"边际贡献"="边际贡献"->"Per_Unit"*"销量";
			ENDFIX
		ENDFIX
		
		FIX("边际贡献","Orig")/*(17)*/
		/*相关日数据聚合到月总量*/		
			FIX("Amount")/*18*/
				@IDESC("YearTotal");
			ENDFIX
			
		/*月单位边际贡献=月总额/月销量*/	
			FIX(@CHILD("YearTotal"))/*(18)*/
				"Per_Unit"="Amount"/"销量"->"Amount";
			ENDFIX	
		ENDFIX
	ENDFIX
	
	/*管财费用当天没有销售收入的情况下将费用分摊到当天不区分上*/
	FIX("no_Product","no_ProductLine","no_Customer","no_Region","no_Channel",@RELATIVE("Entity",0))/*(15)*/
		FIX("税金及附加","管理费用","管理费用-折旧费用","管理费用-薪资及附加","管理费用-工会经费","财务费用","财务费用-利息支出","财务费用-利息收入","财务费用-银行手续费","财务费用-财务服务费","其他业务利润","营业外收支","技术服务费","所得税","财务费用-汇兑损益","财务费用-锁定利率","锁汇资金占用")/*(16)*/
			FIX(&CurDay)/*(17)*/	
				FIX("Amount")/*(18)*/	
				SET CREATENONMISSINGBLK ON;					
					"Orig" (
						IF(@ISMBR("FY21") OR @ISMBR("FY22") OR @ISMBR("FY23"))
							IF(@ISMBR("2月29日"))
								"Orig" = #MISSING;
							ELSE
								IF(	"销售收入"->"Before_Allc"->"All_ProductLine"->"All_Channel"->"All_Product"->"All_Region"->"All_Customer" == #MISSING OR "销售收入"->"Before_Allc"->"All_ProductLine"->"All_Channel"->"All_Product"->"All_Region"->"All_Customer" == 0)
										
									"Orig"= "Before_Allc"->@PARENT("Period")/"Days"->"预算"->"no_Entity"->@PARENT("Period");
										
								ENDIF	
							ENDIF
						ENDIF
					)
				SET CREATENONMISSINGBLK OFF;
				ENDFIX
			ENDFIX

			FIX("Amount","Orig") /*(18)*/
					@IDESC("YearTotal");
			ENDFIX	
		ENDFIX
	ENDFIX
	




/*各维度大聚合,单位=总额/销量*/
	FIX(@IDESC("Yeartotal"),@RELATIVE("AF_P&L",0),@RELATIVE("Quantity",0),"其他业务利润","营业外收支","技术服务费","销售费用","销售费用-运输费用","销售费用-样品费用","销售费用-品牌使用费","管理费用-折旧费用","管理费用-薪资及附加","管理费用-工会经费","财务费用-利息支出","财务费用-利息收入","财务费用-银行手续费","财务费用-财务服务费","产销成本差异","边际贡献","税金及附加","肉类成本","米面成本","蔬菜成本","其他原料成本","辅料成本","包装成本","原料成本","制造费用","直接人工","直接水费","直接电费","直接维修","直接物料消耗","直接蒸汽","直接天然气","直接折旧","直接其他","间接人工","间接水费","间接电费","间接维修","间接物料消耗","间接蒸汽","间接天然气","间接折旧","间接其他","财务费用-汇兑损益","财务费用-锁定利率","所得税","锁汇资金占用","管理费用-跌价准备")/*(11)*/			
		FIX("Amount","Per_Unit","Orig")/*(13)*/
			AGG("Entity");
			AGG("Product");
			AGG("Channel");
			AGG("Customer");
			AGG("Region");
			AGG("ProductLine");
		ENDFIX
	ENDFIX
	FIX(@IDESC("Yeartotal"),"肉类成本","米面成本","蔬菜成本","其他原料成本","辅料成本","包装成本","原料成本","制造费用","直接人工","直接水费","直接电费","直接维修","直接物料消耗","直接蒸汽","直接天然气","直接折旧","直接其他","间接人工","间接水费","间接电费","间接维修","间接物料消耗","间接蒸汽","间接天然气","间接折旧","间接其他")/*(11)*/
		FIX("Per_Unit",@IDESC("Total_Product"),@IDESC("Total_ProductLine"),@RELATIVE("Entity",0),@IDESC("Total_Channel"),@IDESC("Total_Region"),@IDESC("Total_Customer"))/*(18)*/
				"Orig"="Amount"/"产量"->"no_Channel"->"no_Customer"->"no_Region"->"Amount";
		ENDFIX
	ENDFIX		

ENDFIX

