//ESS_LOCALE SimplifiedChinese_China.MS936@Binary
/*
规则内容：每天需要定时执行的脚本
相关表单：
其他注意事项（可选）：
 */

FIX(&EndYr,&CurYr,Fcst,Woking,Periodic,no_CDD03,no_CDD04, no_CDD05) /*7 */
	FIX(@RELATIVE("All_Region",0),@DESCENDANTS("YearTotal"), /*因为前期数据不准确先用全量代替当前日 */@RELATIVE("All_Product",0) , @RELATIVE("All_CDD01",0)) /*11 */
		
		FIX(@RELATIVE(("Ent01"),0))
			FIX("CDD0201"/*生产端利润 */)
				FIX(@RELATIVE("All_Channel",0),"收入","销量")
					"Elim" = "Before_Elim";
				ENDFIX
				FIX(@RELATIVE("All_Channel",0),"成本")
					"Elim" = "收入"->"Before_Elim";
				ENDFIX
			ENDFIX
		ENDFIX
		
        /*动态计算修改为存储，需要手动汇总才能做抵消 */
		FIX("CDD0201"/*生产端利润 */)
			FIX(@RELATIVE("All_Channel",0),"收入","销量","Before_Elim","Elim")
				@IDESC("Ent01");
			ENDFIX
		ENDFIX

		FIX("Ent0199")		
			FIX("CDD0202"/*销售端利润 */)
				
				FIX("CH0101","CH0102","CH0104","CH0105","收入","销量")
					SET CREATENONMISSINGBLK ON;
						"Elim" = "Before_Elim"->"工厂合计";
					SET CREATENONMISSINGBLK OFF;
						
				ENDFIX
				FIX("CH0101","CH0102","CH0104","CH0105","成本")
					SET CREATENONMISSINGBLK ON;
						"Elim" = "收入"->"Before_Elim"->"工厂合计";
					SET CREATENONMISSINGBLK OFF;
				ENDFIX
				
			ENDFIX
		ENDFIX
		
		FIX("Ent1142","Ent04")	
            FIX("CDD0202"/*销售端利润 */)
                FIX("CH0101","CH0102","收入","销量")
                    "Elim" = "Before_Elim";
                ENDFIX
                FIX("CH0101","CH0102","成本")  /*@REMOVE(@RELATIVE("CH01",0),@LIST("CH0105","CH0106")) */
                    "Elim" = "收入"->"Before_Elim";
                ENDFIX
            ENDFIX
        ENDFIX
		
		FIX(@RELATIVE("P&L_Fill",0),@RELATIVE("Quantity",0),@RELATIVE("All_Channel",0),@RELATIVE("CDD02",0),@RELATIVE("All_Entity",0))

			"Orig" = "Before_Elim" - "Elim";
		ENDFIX
			
	ENDFIX
	
	FIX(@RELATIVE("All_Region",0),@RELATIVE("All_Channel",0),@RELATIVE("All_CDD02",0),@RELATIVE("All_CDD01",0),@RELATIVE("Value",0)) /*5 */
		FIX(@RELATIVE("P&L_Fill",0),@RELATIVE("Quantity",0),@DESCENDANTS("YearTotal") )/*因为前期数据不准确先用全量代替当前日) */ /*2 */
			AGG("Product"); /*1 */
			AGG("Entity"); /*1 */
		ENDFIX
	ENDFIX
ENDFIX

/*预估数据拷贝到实际数据 */
FIX(&EndYr,&CurYr,@RELATIVE(&CurMon,0),Woking,Periodic,no_CDD03,no_CDD04, no_CDD05) /*维度数：7 */
	FIX(@RELATIVE("All_Region",0),@IDESCENDANTS("All_Entity"),@RELATIVE("All_Channel",0),@IDESCENDANTS("All_Product"),@RELATIVE("All_CDD02",0),@RELATIVE("Total_CDD01",0)) /*维度数：13 */
		FIX("Orig",@RELATIVE("Quantity",0),@RELATIVE("P&L_Fill",0))  /*维度数：15 */
			DATACOPY "Fcst" to "Actual";
		ENDFIX
	ENDFIX
ENDFIX 